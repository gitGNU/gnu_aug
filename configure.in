#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.56)
#AC_INIT([src/auglocal.h.in])
AC_INIT([aug], [trunk], [mark@emantic.co.uk])

#AM_INIT_AUTOMAKE([aug], [trunk], [mark@emantic.co.uk])
AM_INIT_AUTOMAKE()
AC_CONFIG_HEADER([src/auglocal.h])
AM_MAINTAINER_MODE

# Checks for programs.
AC_PROG_CXX
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL

AC_ARG_ENABLE(enable_python,
    [  --enable-python         enable python support @<:@default=yes@:>@],
    enable_python=$enableval)

AM_CONDITIONAL(ENABLE_PYTHON, [test "$enable_python" != "yes"])

AC_ARG_ENABLE(enable_threads,
    [  --enable-threads        enable thread support @<:@default=yes@:>@],
    enable_threads=$enableval)

# Checks for libraries.

LDFLAGS="-lm $LDFLAGS"

if test "$enable_threads" != "no"; then
   CFLAGS="-D_MT -D_REENTRANT $CFLAGS"
   CXXFLAGS="-D_MT -D_REENTRANT $CXXFLAGS"
fi

case $host in
*-*-cygwin* | *-*-mingw*)

    # cp c:/WINNT/system32/ssleay32.dll c:/WINNT/system32/libssleay32.dll 
    # cd c:/OpenSSL/lib/MinGW/
    # cp ssleay32.a libssleay32.a
    # cp lib*.a /lib/mingw/

    AC_SEARCH_LIBS(SSL_library_init, [ssleay32])
    AC_SEARCH_LIBS(BIO_new, [eay32])

    CFLAGS="$CFLAGS -DWINVER=0x0501 -DFD_SETSIZE=256"
    CXXFLAGS="$CXXFLAGS -DWINVER=0x0501 -DFD_SETSIZE=256"
    LDFLAGS="-lws2_32 -liphlpapi $LDFLAGS"
    LIBS="-Wl,-lws2_32 -Wl,-liphlpapi $LIBS"

    if test "$enable_threads" != "no"; then
        CFLAGS="-mthreads $CFLAGS"
        CXXFLAGS="-mthreads $CXXFLAGS"
        LDFLAGS="-mthreads $LDFLAGS"
    fi
    ;;
*)
    AC_SEARCH_LIBS(SSL_library_init, [ssl])
    AC_SEARCH_LIBS(BIO_new, [crypto])

    AC_SEARCH_LIBS(gethostbyname, nsl)
    AC_SEARCH_LIBS(gethostname, nsl)
    AC_SEARCH_LIBS(socket, socket)
    AC_SEARCH_LIBS(dlopen, [dl dlcompat])

    if test "$enable_threads" != "no"; then
        AC_SEARCH_LIBS(pthread_create, [pthread])
    fi
    ;;
esac

# Checks for libraries.

# Checks for header files.
AC_FUNC_ALLOCA
AC_CHECK_HEADERS([dlfcn.h sys/sockio.h openssl/err.h openssl/ssl.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_BIGENDIAN

AC_MSG_CHECKING(for socklen_t)
AC_TRY_COMPILE([
#include <sys/types.h>
#if !defined(_WIN32)
# include <sys/socket.h>
# include <unistd.h>
#else /* _WIN32 */
# include <winsock2.h>
#endif /* _WIN32 */
socklen_t x;
], [], [AC_MSG_RESULT(yes)], [
    AC_TRY_COMPILE([
#include <sys/types.h>
#if !defined(_WIN32)
# include <sys/socket.h>
#else /* _WIN32 */
# include <winsock2.h>
#endif /* _WIN32 */
int accept(int, struct sockaddr*, size_t*);
    ], [], [
        AC_MSG_RESULT(size_t)
        AC_DEFINE(socklen_t, size_t, [socklen_t size])
    ], [
        AC_MSG_RESULT(int)
        AC_DEFINE(socklen_t, int, [socklen_t size])
    ])
])

AC_MSG_CHECKING(for ipv6)
AC_TRY_COMPILE([
    # include <sys/types.h>
    #if !defined(_WIN32)
    # include <sys/socket.h>
    # include <netinet/in.h>
    # include <arpa/inet.h>
    # include <netdb.h>
    #else /* _WIN32 */
    # include <winsock2.h>
    # include <ws2tcpip.h>
    #endif /* _WIN32 */
    int x = IPV6_ADD_MEMBERSHIP;
], [], [
	AC_MSG_RESULT(yes)
	AC_DEFINE(HAVE_IPV6, 1, [Define to 1 if you have ipv6 support.])
], [
	AC_MSG_RESULT(no)
])

AC_MSG_CHECKING(whether the compiler supports __func__)
AC_TRY_COMPILE([
const char*
func()
{
    return __func__;
}
], [], [
    AC_MSG_RESULT(yes)
], [
    AC_MSG_RESULT(no)
    AC_MSG_CHECKING(whether the compiler supports __FUNCTION__)
    AC_TRY_COMPILE([
const char*
func()
{
    return __FUNCTION__;
}
    ], [], [
        AC_MSG_RESULT(yes)
        AC_DEFINE(__func__, __FUNCTION__,
            [Define to the equivalent of __func__])
    ], [
        AC_MSG_RESULT(no)
        AC_DEFINE(__func__, "unknown",
            [Define to the equivalent of __func__])
    ])
])

# Checks for library functions.
AC_CHECK_FUNCS([bzero localtime_r poll strlcpy timegm])

AC_CONFIG_FILES([Makefile
                 doc/Makefile
                 doc/man/Makefile
                 etc/Makefile
                 example/Makefile
                 example/augas/Makefile
                 example/augas/etc/Makefile
                 example/augas/htdocs/Makefile
                 example/cpp/Makefile
                 src/Makefile
                 src/c/Makefile
                 src/c/augmar/Makefile
                 src/c/augnet/Makefile
                 src/c/augpy/Makefile
                 src/c/augres/Makefile
                 src/c/augsrv/Makefile
                 src/c/augsrv/posix/Makefile
                 src/c/augsrv/win32/Makefile
                 src/c/augsys/Makefile
                 src/c/augsys/osx/Makefile
                 src/c/augsys/posix/Makefile
                 src/c/augsys/win32/Makefile
                 src/c/augutil/Makefile
                 src/c/augutil/posix/Makefile
                 src/c/augutil/win32/Makefile
                 src/c/htdigest/Makefile
                 src/c/mar/Makefile
                 src/cpp/Makefile
                 src/cpp/augmarpp/Makefile
                 src/cpp/augnetpp/Makefile
                 src/cpp/augsrvpp/Makefile
                 src/cpp/augsyspp/Makefile
                 src/cpp/augutilpp/Makefile
                 src/cpp/daug/Makefile
                 src/cpp/daug/etc/Makefile
                 src/pl/Makefile
                 src/py/Makefile
                 src/sh/Makefile
                 vc7/Makefile])
AC_OUTPUT
